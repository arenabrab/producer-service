plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id 'au.com.dius.pact' version '4.3.5'
    id 'groovy'
    id 'java'
}

group = 'com.example'
sourceCompatibility = '17'

scmVersion {
    def versionSha = System.getenv("GITHUB_SHA")
    versionSha = versionSha ? "-${versionSha.substring(0, 8)}" : ""
    snapshotCreator = { version, position -> "-" + position.getBranch() + versionSha }
    tag {
        prefix = 'provider-service-'
    }
}

version = scmVersion.version


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux:2.6.3'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.3.5'
    testImplementation 'org.spockframework:spock-spring:2.1-groovy-3.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.3'
    testImplementation 'io.projectreactor:reactor-test:3.4.14'
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperties['pact.verifier.publishResults'] = System.getProperty("GITHUB_CI") ?: false
    systemProperties['pact.provider.version'] = scmVersion.version
}

pact {
    broker {
        pactBrokerUrl = "https://squaretrade.pactflow.io"
        pactBrokerToken = "YHsHQiENwpB5w4EcYG_p_A"
    }
}
